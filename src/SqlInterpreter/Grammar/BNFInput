option StartSymbol = <Statement>
option LineComment = '#'

Identifier = {Letter} {LetterOrDigit}*
Number = {Digit}+ ('.' {Digit}+)?
String = \' {StringLetter}* \'

{NotAllowed} = [\' \n]
{StringLetter} = {Any - NotAllowed}

<ValueExpression> =
    @Number Number |
    @String String |
    <ObjectValue> |
    @Null null |
    @True true |
    @False false |
    <MemberAccess> |
    @Round round '(' <Expression> ')' |
    @Abs abs '(' <Expression> ')' |
    @Ceiling ceiling '(' <Expression> ')' |
    @Floor floor '(' <Expression> ')' |
    @Max2 max '(' <Expression> ',' <Expression> ')' |
    @Min2 min '(' <Expression> ',' <Expression> ')' |
    @Sign sign '(' <Expression> ')' |
    @Trim trim '(' <Expression> ')' |
    @Contains contains '(' <Expression> ',' <Expression> ')' |
    @Any any '(' <Select> ')' |
    @Sum sum '(' <Select> ')' |
    @Max1 max '(' <Select> ')' |
    @Min1 min '(' <Select> ')' |
    @Avg avg '(' <Select> ')' |
    @Not not '(' <Expression> ')' |
    '(' <Expression> ')'

<ObjectValue> = @ObjectValue '{' <ObjectFieldList> '}'
<ObjectField> = Identifier ':' <Expression>
<ObjectFieldList> = <ObjectField> ',' <ObjectFieldList> | <ObjectField>

<MemberAccess> = @MemberAccess <MemberAccess> '.' Identifier | @Identifier Identifier

<Expression> = @And <Expression> and <RelationalExpression> |
    @Or <Expression> or <RelationalExpression> |
    <RelationalExpression>

<RelationalExpression> = @Eq <RelationalExpression> '=' <AddExpression> |
    @NotEq <RelationalExpression> '!=' <AddExpression> |
    @Lt <RelationalExpression> '<' <AddExpression> |
    @Gt <RelationalExpression> '>' <AddExpression> |
    @LtEq <RelationalExpression> '<=' <AddExpression> |
    @GtEq <RelationalExpression> '>=' <AddExpression> |
    <AddExpression>

<AddExpression> = @Add <AddExpression> '+' <MultExpression> |
    @Sub <AddExpression> '-' <MultExpression> |
    <MultExpression>

<NegateExpression> = @Neg '-' <ValueExpression> 
               | <ValueExpression>

<MultExpression> = @Mult <MultExpression> '*' <NegateExpression> |
    @Div <MultExpression> '/' <NegateExpression> |
    <NegateExpression>

<FromSelect> = @FromSelect from Identifier in <FromSource> 
<FromSource> = @FromSourceId Identifier | @FromSourceSel '(' <Select> ')'
<FromSelectList> = <FromSelect> <FromSelectList> | <FromSelect>

<WhereClause> = @WhereFilter where <Expression>

<GroupBySelector> = <Expression> as Identifier
<GroupBySelectorList> = <GroupBySelector> ',' <GroupBySelectorList> | <GroupBySelector>
<GroupByClause> = @GroupByClause groupby <GroupBySelectorList> into Identifier

<OrderBySelect> = <Expression> | <Expression> asc | <Expression> desc 
<OrderBySelectList> = <OrderBySelect> ',' <OrderBySelectList> | <OrderBySelect>
<OrderByClause> = @OrderByClause orderby <OrderBySelectList>

<SelectClause> = @SelectClause select <Expression>

<WhereGroupByWhere> = @WhereGroupByHaving <WhereClause> <GroupByClause> <WhereClause> | 
    @WhereGroupBy <WhereClause> <GroupByClause> | 
    @GroupByHaving <GroupByClause> <WhereClause> | 
    @Where <WhereClause> | 
    @GroupBy <GroupByClause> |
    <>

<OptionalOrderByClause> = <OrderByClause> | <>

<Select> = @Select <FromSelectList> <WhereGroupByWhere> <OptionalOrderByClause> <SelectClause>

<Insert> = @InsertValue insert into Identifier <ObjectValue> | @InsertSelect insert into Identifier '(' <Select> ')'
 
<Delete> = @DeleteAll delete from Identifier | @DeleteWhere delete from Identifier where <Expression>

<Statement> = @IsSelect <Select> | @IsInsert <Insert> | @IsDelete <Delete>








